
class: center, middle


Llenguatges de Programaci√≥

# Haskell - Part 4

Albert Rubio, Jordi Petit

<br/>

![:height 10em](img/haskell.svg)

<br/>

Universitat Polit√®cnica de Catalunya, 2019

---

# Tipus predefinits

Ja hem vist que existeixen una seÃÄrie de tipus predefinits:

- Tipus simples:
    - `Int`, `Integer`, `Float`, `Double`
    - `Bool`
    - `Char`

- Tipus estructurats:
    - Llistes
    - Tuples
    - Funcions

```haskell
     5  :: Integer
   True :: Bool
    'a' :: Char
[1,2,3] :: [Integer]
('b',4) :: (Char,Integer)
    not :: Bool -> Bool
```


---

# Tipus polim√≤rfics

```haskell
length :: [a] -> Int
map :: (a -> b) -> [a] -> [b]
```

El **polimorfisme param√®tric**  √©s un mecanisme senzill que permet
definir funcions (i tipus) que s'escriuen gen√®ricament, sense
dependre dels tipus dels objectes sobre els quals s'apliquen.

En Haskell, les **variables de tipus** poden prendre qualsevol valor
i estan quantificades universalment. Per convenci√≥ `a`, `b`, `c`, ...



---

# Tipus polim√≤rfics


Per a utilitzar funcions amb tipus polimoÃÄrfics cal que hi hagi
una substitucioÃÅ de les variables de tipus que s‚ÄôadequÃài a l‚ÄôaplicacioÃÅ que estem fent.

**Exemple:**  `map even [3,6,1]` t√© tipus `[Bool]` ja que:

- el tipus de `map` √©s `(a -> b) -> [a] -> [b]`,
- el tipus de `even` √©s `Int -> Bool`,
- per tant, `a` es pot substituir per `Int` i `b` es pot substituir per `Bool`,
- i el tipus final de l'expressi√≥ √©s `[Bool]`.

<br>

Una expressioÃÅ d√≥na error de tipus si no existeix una substitucioÃÅ
per a les seves variables de tipus.

**Exemple:** `map not ['b','c']` doÃÅna error de tipus ja que:

- per una banda, `a` hauria de ser `Bool`,
- per altre banda, `a` hauria de ser `Char`.



---

# Tipus sin√≤nims

La construcci√≥ `type` permet substituir un tipus (complex)
per un nou nom.

Els dos tipus soÃÅn intercanviables.

```haskell
type Euros = Float

sou :: Persona -> Euros
```

```haskell
type Diccionari = String -> Int

crear :: Diccionari
cercar :: Diccionari -> String -> Int
inserir :: Diccionari -> String -> Int -> Diccionari
esborrar :: Diccionari -> String -> Diccionari
```

Els tipus sin√≤nims aporten claredat (per√≤ no m√©s seguretat).

üí° Per a m√©s seguretat, mireu `newtype` (no el considerem).

---

# Tipus enumerats

Els **tipus enumerats** donen la llista de valors possibles
dels objectes d'aquell tipus.

```haskell
data Jugada = Pedra | Paper | Tisores

data Operador
    = Suma
    | Resta
    | Producte
    | Divisio

data Bool = False | True    -- predefinit
```

Els valors enumerats (**constructors**), han de comen√ßar amb maj√∫scula.

Els tipus enumerats es poden desconstruir amb patrons:

```haskell
guanya :: Jugada -> Jugada -> Bool
    -- diu si la primera jugada guanya a la segona

guanya Paper Pedra = True
guanya Pedra Tisores = True
guanya Tisores Paper = True
guanya _ _ = False
```



---

# Tipus algebraics



Els **tipus algebraics** defineixen diversos constructors,
cadascun amb zero o m√©s dades associades.


```haskell
data Forma
    = Rectangle Float Float         -- al√ßada, amplada
    | Quadrat Float                 -- mida
    | Cercle Float                  -- radi
    | Punt
```

Les dades es creen especificant el constructor i els seus valors respectius:

```haskell
Œª> r = Rectangle 3 4
Œª> :type r
üëâ r :: Forma

Œª> c = Cercle 2.0
Œª> :type c
üëâ c :: Forma
```


---

# Tipus algebraics

```haskell
data Forma
    = Rectangle Float Float         -- al√ßada, amplada
    | Quadrat Float                 -- mida
    | Cercle Float                  -- radi
    | Punt
```

Els tipus algebraics es poden desconstruir amb patrons:

```haskell
area :: Forma -> Float

area (Rectangle amplada al√ßada) = amplada * al√ßada
area (Quadrat mida) = area (Rectangle mida mida)
area (Cercle radi) = pi * radi^2
area Punt = 0
```

```haskell
Œª> area (Rectangle 3 4)
üëâ 12

Œª> c = Cercle 2.0
Œª> area c
üëâ 12.566370614359172
```

---

# Tipus algebraics

Per escriure valors algebraics, cal afegir `deriving (Show)` al final
del tipus.
<br>‚üπ m√©s endavant veurem qu√® vol dir.

```haskell
data Punt = Punt Int Int
    deriving (Show)

data Rectangle = Rectangle Punt Punt
    deriving (Show)
```

```haskell
Œª> p1 = Punt 2 3
Œª> p1
üëâ Punt 2 3

Œª> p2 = Punt 4 6
Œª> p2
üëâ Punt 4 6

Œª> r = Rectangle p1 p2
Œª> r
üëâ Rectangle (Punt 2 3) (Punt 4 6)
```

---

# Arbres binaris d'enters

Els tipus algebraics tamb√© es poden definir recursivament!

```haskell
data Arbin = Buit | Node Int Arbin Arbin
    deriving (Show)
```

```haskell
Œª> a1 = Node 1 Buit Buit
Œª> a2 = Node 2 Buit Buit
Œª> a3 = Node 3 a1 a2
Œª> a4 = Node 4 a3 Buit
Œª> a4
üëâ Node 4 (Node 3 (Node 1 Buit Buit) (Node 2 Buit Buit)) Buit

Œª> a5 = Node 5 a4 a4        -- I üíú sharing
Œª> a5
üëâ Node 5 (Node 4 (Node 3 (Node 1 Buit Buit) (Node 2 Buit Buit)) Buit) (Node 4 (Node 3 (Node 1 Buit Buit) (Node 2 Buit Buit)) Buit)
```

Com sempre, la desconstrucci√≥ via patrons marca el cam√≠: üë£

```haskell
alcada :: Arbin -> Int

alcada Buit = 0
alcada (Node _ fe fd) = 1 + max (alcada fe) (alcada fd)
```

---

# Arbres binaris gen√®rics

Els tipus algebraics tamb√© tenen polimorfisme param√®tric!

```haskell
data Arbin a = Buit | Node a (Arbin a) (Arbin a)
    deriving (Show)
```

```haskell
a1 :: Arbin Int
a1 = Node 3 (Node 1 Buit Buit) (Node 2 Buit Buit)

a2 :: Arbin Forma
a2 = Node (Rectangle 3 4) (Node (Cercle 2) Buit Buit) (Node Punt Buit Buit)
```

```haskell
alcada :: Arbin a -> Int

alcada Buit = 0
alcada (Node _ fe fd) = 1 + max (alcada fe) (alcada fd)
```


```haskell
preordre :: Arbin a -> [a]

preordre Buit = []
preordre (Node x fe fd) = [x] ++ preordre fe ++ preordre fd
```

---

# Arbres generals gen√®rics

```haskell
data Argal a = Argal a [Argal a]        -- (no hi ha arbre buit en els arbres generals)
    deriving (Show)
```

```haskell
a = Argal 4 [Argal 1 [], Argal 2 [], Argal 3 [Argal 0 []]]
```

```haskell
mida :: Argal a -> Int

mida (Argal _ fills) = 1 + sum (map mida fills)
```


```haskell
preordre :: Argal a -> [a]

preordre (Argal x fills) = x : concatMap preordre fills
```



---

# Expressions booleanes amb variables

```haskell
data ExprBool
    = Val Bool
    | Var Char
    | Not ExprBool
    | And ExprBool ExprBool
    | Or  ExprBool ExprBool
    deriving (Show)

type Dict = Char -> Bool
```

```haskell
eval :: ExprBool -> Dict -> Bool

eval (Val x) d = x
eval (Var v) d = d v
eval (Not e) d = not $ eval e d
eval (And e1 e2) d = eval e1 d && eval e2 d
eval (Or  e1 e2) d = eval e1 d || eval e2 d
```


```haskell
e = (And (Or (Val False) (Var 'x')) (Not (And (Var 'y') (Var 'z'))))
d = (`elem` "xz")
eval e d
    -- evalua (F ‚à® x) ‚àß (¬¨ (y ‚àß z)) amb x = z = T i y = F
```


---

# Llistes gen√®riques

```haskell
data Llista a = Buida | a `DavantDe` (Llista a)
```

```haskell
l1 = 3 `DavantDe` 2 `DavantDe` 4 `DavantDe` Buida
```

```haskell
llargada :: Llista a -> Int

llargada Buida = 0
llargada (cap `DavantDe` cua) = 1 + llargada cua
```

--

Les llistes de Haskell s√≥n exactament aix√≤!
(amb una mica de sucre sint√†ctic üç¨)


```haskell
data [a] = [] | a : [a]
```

```haskell
l1 = 3:2:4:[]    -- l1 = [3, 2, 4]
```

```haskell
length :: [a] -> Int

length [] = 0
length (x:xs) = 1 + length xs
```



---

# Maybe a

El tipus polim√≤rfic `Maybe a` estaÃÄ predefinit aix√≠:

```haskell
data Maybe a = Just a | Nothing
```

Expressa dues possibilitats:

- la pres√®ncia d'un valor (de tipus `a` amb el constructor `Just`), o
- la seva abs√®ncia (amb el constructor buit `Nothing`).

Aplicacions:

- Indicar possibles valor nuls.
- Indicar abs√®ncia d'un resultat.
- Reportar un error.

Exemples: (busqueu doc a [HoogŒªe](https://www.haskell.org/hoogle/))

```haskell
find :: (a -> Bool) -> [a] -> Maybe a
    -- cerca en una llista amb un predicat

lookup :: Eq a => a -> [(a,b)] -> Maybe b
    -- cerca en una llista associativa
```





---

# Either a b

El tipus polim√≤rfic `Either a b` estaÃÄ predefinit aix√≠:

```haskell
data Either a b = Left a | Right b
```

Expressa dues possibilitats per un valor:

- un valor de tipus `a` (amb el constructor `Left`), o
- un valor de tipus `b` (amb el constructor `Right`).

Aplicacions:

- Indicar que un valor pot ser, alternativament, de dos tipus.
- Reportar un error. Habitualment:
    - `a` √©s un `String` i √©s el diagn√≤stic de l'error.
    - `b` √©s del tipus del resultat esperat.
    - **Mnemot√®cnic:** *right* vol dir *dreta* i tamb√© *correcte*.

Exemple:

```haskell
secDiv :: Float -> Float -> Either String Float
secDiv _ 0 = Left "divisi√≥ per zero"
secDiv x y = Right (x / y)
```


---

# Perspectiva


```haskell
data Expr a
    = Val a
    | Var String
    | Neg (Expr a)
    | Sum (Expr a) (Expr a)
    | Res (Expr a) (Expr a)
    | Mul (Expr a) (Expr a)
    | Div (Expr a) (Expr a)
```

Com seria en C++?


---

# Perspectiva

.cols5050[
.col1[
```c++
template <typename a> class Expr {

    struct ValData {
        a x;
    };

    struct VarData {
        string v;
    };

    struct NegData {
        Node* e;
    };

    struct OpData {
        Node* e1;
        Node* e2;
    };

    enum Constructor {Val, Var, Neg,
            Sum, Res, Mul, Div};
```
]
.col2[
```c++
    struct Node {
        Constructor c;
        union {
            ValData val;
            VarData var;
            NegData neg;
            OpData  op;
        };
    };

    Node* p; // punter al node amb l'expressi√≥

public:

    Expr ExprVal (const a& x);
    Expr ExprVar (const string& v);
    Expr ExprNeg (const Expr& e);
    Expr ExprSum (const Expr& e1,
                  const Expr& e2);
    ...
};
```
]
]

--

I encara falten les operacions i la gesti√≥ de la mem√≤ria!

.xxl[üò∞üßü]

---

# Exercicis

- Feu aquests problemes de Jutge.org:

    - [P97301](https://jutge.org/problems/P97301) FizzBuzz
    - [P37072](https://jutge.org/problems/P37072) Arbre binari
    - [P87706](https://jutge.org/problems/P87706) Arbres binaris de cerca
    - [P80618](https://jutge.org/problems/P80618) Cua 1 (nom√©s el primer apartat)
    - [P79515](https://jutge.org/problems/P79515) Arbres AVL (üèÖ)
    - [P92181](https://jutge.org/problems/P92181) Nombres pseudoperfectes (üèÖ)


---

# Classes de tipus

Una **classe de tipus** (*type class*) √©s una interf√≠cie que defineix un comportament.

Els tipus poden **instanciar** (implementar seguint la interf√≠cie)
una o m√©s classes de tipus.

La instanciaci√≥ es pot fer

- autom√†ticament pel compilador per a certes classes predefinides, o
- a m√†.

Les classes de tipus

- s√≥n la forma de tenir sobrecaÃÄrrega en Haskell, i
- proporcionen una altra forma de polimorfisme.

<br>
<br>
‚ö†Ô∏è Les classes de tipus de Haskell no s√≥n classes de OOP com a C++ o Java
(m√©s aviat s√≥n com els `interface`s de Java).




---

# La classe `Eq`

La funci√≥ `elem` necessita comparar elements per igualtat:

```haskell
elem :: (Eq a) => a -> [a] -> Bool

elem x [] = False
elem x (y:ys) = x == y || elem x ys
```

La declaraci√≥ `(Eq a) =>` indica que els tipus `a`
sobre els quals es pot aplicar la funci√≥ `elem`
han de ser inst√†ncies de la classe `Eq`.

La classe predefinida `Eq`
d√≥na operacions d'igualtat i desigualtat:

```haskell
class Eq a where
    (==) :: a -> a -> Bool
    (/=) :: a -> a -> Bool
```

I fins i tot ja proporciona definicions per defecte (circulars, qu√® hi farem!):

```haskell
class Eq a where
    (==) :: a -> a -> Bool
    (/=) :: a -> a -> Bool

    x == y  = not (x /= y)
    x /= y  = not (x == y)
```



---

# La classe `Eq`

El nostre tipus `Jugada` (encara) no d√≥na suport a la classe `Eq`:

```haskell
data Jugada = Pedra | Paper | Tisora

Œª> Paper /= Paper
üí£ error: "No instance for (Eq Jugada) arising from a use of ‚Äò/=‚Äô"

Œª> Pedra `elem` [Paper, Pedra, Paper]
üí£ error: "No instance for (Eq Jugada) arising from a use of ‚Äòelem‚Äô"
```

Amb `deriving (Eq)` demanem al compilador que
instanci√Ø autom√†ticament la classe `Eq` (usant igualtat estructural):


```haskell
data Jugada = Pedra | Paper | Tisora
    deriving (Eq)

Œª> Paper /= Paper
üëâ False

Œª> Pedra `elem` [Paper, Pedra, Paper]
üëâ True
```


---

# La classe `Eq`

Per alguns tipus, la igualtat estructural no √©s suficient:

```haskell
data Racional = Racional Int Int        -- numerador, denominador
    deriving (Eq)

Œª> Racional 3 2 == Racional 6 4
üëé False
```

En aquests casos cal instanciar la classe a m√†:

```haskell
instance Eq Racional where
    (Racional n1 d1) == (Racional n2 d2) = n1 * d2 == n2 * d1

Œª> Racional 3 2 == Racional 6 4
üëç True

Œª> Racional 3 2 /= Racional 6 4
üëç False
```

Nom√©s cal definir `==` perqu√® la definici√≥ per defecte de `/=` ja ens conv√©.

---

# La classe `Eq`

Per alguns tipus, instanciar una classe tamb√© requereix alguna altra classe:

```haskell
data Arbin a = Buit | Node a (Arbin a) (Arbin a)

instance Eq a => Eq (Arbin a) where

    Buit == Buit = True
    (Node x1 fe1 fd1) == (Node x2 fe2 fd2) = x1 == x2 && fe1 == fe2 && fd1 == fd2
    _ == _ = False
```


---

# Informaci√≥ sobre inst√†ncies

Amb la comanda `:info T` (o `:i T`) de l'int√®rpret es pot veure de quines
classes √©s inst√†ncia un tipus `T`:

```haskell
Œª> :i Racional
data Racional = Racional Int Int
*instance Eq Racional

Œª> :i Int
data Int = GHC.Types.I# GHC.Prim.Int#
*instance Eq Int
instance Ord Int
instance Show Int
instance Read Int
instance Enum Int
instance Num Int
instance Real Int
instance Bounded Int
instance Integral Int
```


---

# La classe `Ord`

La classe predefinida `Ord` (que requereix la classe `Eq`)
d√≥na operacions d'ordre:

```haskell
data Ordering = LT | EQ | GT        -- possibles resultats d'una comparaci√≥ d'ordre

class (Eq a) => Ord a where
    compare               :: a -> a -> Ordering
    (<), (<=), (>=), (>)  :: a -> a -> Bool
    max, min              :: a -> a -> a

    compare x y
        | x == y    = EQ
        | x <= y    = LT
        | otherwise = GT
    x <  y = compare x y == LT
    x >  y = compare x y == GT
    x <= y = compare x y /= GT
    x >= y = compare x y /= LT
```

El miÃÅnim que cal per fer la instanciaci√≥ eÃÅs definir el `<=` o el `compare`.

Tot i que no es verifica, s'espera que les inst√†ncies d'`Ord` compleixin
aquestes lleis:

- Transitivitat: si `x <= y && y <= z` llavors `x <= z`.
- Reflexivitat: `x <= x`.
- Antisimetria: si `x <= y && y <= x` llavors `x == y`.



---

# La classe `Show`

La classe predefinida `Show` d√≥na suport per convertir valors en textos:

```haskell
class Show a where
    show :: a -> String
```

Amb `deriving (Show)`, el compilador la ofereix autom√†ticament (usant
sintaxi Haskell):

```haskell
data Racional = Racional Int Int        -- numerador, denominador
    deriving (Eq, Show)

Œª> show $ Racional 3 2  üëâ "Racional 3 2"
Œª> show $ Racional 6 4  üëâ "Racional 6 4"    üíî
```

Alternativament, per fer la instanciaci√≥ a m√† nom√©s cal definir el `show`:


```haskell
instance Show Racional where
    show (Racional n d) = (show $ div n m) ++ "/" ++ (show $ div d m)
        where m = gcd n d

Œª> show $ Racional 3 2  üëâ "3/2"
Œª> show $ Racional 6 4  üëâ "3/2"             üíñ
```

---

# La classe `Read`

La classe predefinida `Read` d√≥na suport per convertir textos en valors:

```haskell
class Read a where
    read :: String -> a
```

Amb `deriving (Read)`, el compilador la ofereix autom√†ticament (usant
sintaxi Haskell).

Alternativament, per fer la instanciaci√≥ a m√† nom√©s cal definir el `read`.

**Compte:** Al usar `read`, sovint cal especificar el tipus de retorn, perqu√®
el compilador s√†piga a quin de tots els `read`s sobrecarregats ens referim:

```haskell
Œª> read "38"                    üí£ "Exception: Prelude.read: no parse"
Œª> (read "38") :: Int           üëâ 38
Œª> (read "38") :: Integer       üëâ 38
Œª> (read "38") :: Float         üëâ 38.0
```


---

# La classe `Num`

La classe predefinida `Num` d√≥na suport a operadors aritm√®tics b√†sics:

```haskell
class (Eq a, Show a) => Num a where
    (+), (-), (*)       :: a -> a -> a
    negate, abs, signum :: a -> a
    fromInteger         :: Integer -> a

    x - y    = x + negate y
    negate x = 0 -x
```

Per fer la instanciaci√≥ cal definir totes les operacions
menys `negate` o `-`.

Els tipus `Int`, `Integer`, `Float` i `Double` soÃÅn inst√†ncies de la classe `Num`.


---

# Altres classes predefinides

.center[
![:height 24em](img/haskell_standard_classes.png)

.xxs[Imatge: http://continuation.passing.style/2015/12/21/not-gentle-intro-to-haskell/]
]


---

# UÃÅs de classes en declaracions de tipus

```haskell
suma [] = 0
suma (x:xs) = x + suma xs
```

Quin √©s el tipus de `suma`?

--

```haskell
suma :: [Int] -> Int
```

--

.center[‚ùå m√©s general!]

--

```haskell
suma :: [a] -> a
```

--

.center[‚ùå el tipus `a` no pot ser qualsevol: ha de tenir l'operaci√≥ `+`!]

--

```haskell
suma :: Num a => [a] -> a
```

--

.center[‚úÖ el tipus `a` ha de ser inst√†ncia de `Num`!]

--

Les condicions sobre les variables de tipus
es posen davant de `=>` a la signatura.

El sistema de tipus de Haskell √©s capa√ß d'inferir tipus i condicions autom√†ticament.
<br>‚üπ m√©s endavant veurem com.


---

# Definici√≥ de classes pr√≤pies

Nom√©s cal utilitzar la mateixa sintaxi que ja hem vist.

**Exemple:** Classe per a predicats.

```haskell
class Pred a where
    sat   :: a -> Bool
    unsat :: a -> Bool

    unsat = not . sat
```

Instanciaci√≥ pels enters:

```haskell
instance Pred Int where
    sat 0 = False
    sat _ = True
```

Instanciaci√≥ pels arbres binaris:

```haskell
instance Pred a => Pred (Arbin a) where
    sat Buit = True
    sat (Node x fe fd) = sat x && sat fe && sat fd
```

---

# Exercicis

- Feu aquests problemes de Jutge.org:

    - [P80618](https://jutge.org/problems/P80618) Cua 1 (tot)

